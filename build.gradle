buildscript {
    configurations.all {
        // check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
    }
}
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'nexus'

repositories{
  mavenLocal()
  mavenCentral()
  jcenter()
}

group='com.github.signed.gradle.plugin'
version='0.0.2-SNAPSHOT'
sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile(
            ['org.hamcrest:hamcrest-generator:1.3']
          , ['com.google.code.javaparser:javaparser:1.0.11']
          , ['com.google.guava:guava:18.0']
          , ['commons-io:commons-io:2.3']
    )
    testCompile(
            ['org.hamcrest:hamcrest-core:1.3']
          , ['org.hamcrest:hamcrest-library:1.3']
          , ['junit:junit:4.11']
          , ['org.mockito:mockito-core:1.9.0']
          , ['com.sun.codemodel:codemodel:2.6']
    )
    testCompile ('org.spockframework:spock-core:0.7-groovy-2.0') {
        exclude group: 'org.codehaus.groovy'
    }
    compile gradleApi()
    compile localGroovy()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.0'
}

modifyPom {
    project {
        name 'Gradle Hamcrest Plugin'
        description 'Gradle plugin that generates a central class to access your custom matchers.'
        url 'https://github.com/signed/gradle-hamcrest-plugin'
        inceptionYear '2012'

        scm {
            url 'https://github.com/signed/gradle-hamcrest-plugin'
            connection 'scm:https://github.com/signed/gradle-hamcrest-plugin.git'
            developerConnection 'scm:git@github.com:signed/gradle-hamcrest-plugin.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'thomasheilbronner'
                name 'Thomas Heilbronner'
            }
        }
    }
}

def environments = [
        'local': [
                repositoryLogin: deploymentNexusLocalLogin,
                repositoryPassword: deploymentNexusLocalPassword,
                'urls': [
                        'snapshot': 'http://localhost:8081/nexus/content/repositories/snapshots',
                        'release' : 'http://localhost:8081/nexus/content/repositories/releases'

                ]
        ],
        'sonatype': [
                repositoryLogin: deploymentSonatypeLogin,
                repositoryPassword: deploymentSonatypePassword,
                'urls': [
                        'snapshot': 'https://oss.sonatype.org/content/repositories/snapshots',
                        'release' : 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                ]
        ]
]

def intent = hasProperty('release') ? 'release' : 'snapshot'
def target = hasProperty('sonatype') ? 'sonatype' : 'local'
println "intent: $intent target: $target url: ${environments.get(target).urls.get(intent)}"

def environment = environments.get(target)
if (project.hasProperty('release')) {
    project.version -= '-SNAPSHOT'
}

ext.nexusUsername = environment.repositoryLogin
ext.nexusPassword = environment.repositoryPassword

nexus {
    sign = true
    repositoryUrl = environment.urls.release
    snapshotRepositoryUrl = environment.urls.snapshot
}
